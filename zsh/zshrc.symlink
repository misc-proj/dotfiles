#
# Executes commands at the start of an interactive session
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

fpath=($ZSH/functions $fpath)

source $ZSH/system/path.zsh

bundle_init=$ZSH/zsh/bundle.init

update-bundles() {(
  local bundles
  local bundle
  local files
  local file

  bundles=("${(@f)$(cat $ZSH/zsh/bundle.list)}")

  cd $ZSH

  for bundle in $bundles; do
    if [ ! -d "$ZSH/zsh/bundles/$bundle" ]; then
      git submodule add https://github.com/$bundle zsh/bundles/$bundle
    fi
  done

  git submodule update --init --recursive
  rm -f $bundle_init
  touch $bundle_init

  for bundle in $bundles; do
    files=zsh/bundles/$bundle/*.plugin.zsh
    for file in $~files; do
      echo source \$ZSH/$file >> $bundle_init
    done
  done

  echo >> $bundle_init

  for bundle in $bundles; do
    echo fpath+=\$ZSH/zsh/bundles/$bundle >> $bundle_init
  done
)}

if [ -r "$bundle_init" ]; then
  source $bundle_init
fi

autoload -U $ZSH/functions/*(:t)

# enable both the editor and autosuggestions plugin

# Enables terminal application mode and updates editor information.
function zle-line-init {
  # enable zsh-autosuggestions
  zle autosuggest-start

  # The terminal must be in application mode when ZLE is active for $terminfo
  # values to be valid.
  if (( $+terminfo[smkx] )); then
    # Enable terminal application mode.
    echoti smkx
  fi

  # Update editor information.
  zle editor-info
}

zle -N zle-line-init

# accept autosuggestions without leaving insert mode
bindkey '^f' vi-forward-blank-word

AUTOSUGGESTION_ACCEPT_RIGHT_ARROW=1
